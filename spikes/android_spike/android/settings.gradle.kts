pluginManagement {
    if (!providers.gradleProperty("hotfix.prebuilt").isPresent) {
        val flutterSdkPath =
        run {
            val properties = java.util.Properties()
            file("local.properties").inputStream().use { properties.load(it) }
            val flutterSdkPath = properties.getProperty("flutter.sdk")
            require(flutterSdkPath != null) { "flutter.sdk not set in local.properties" }
            flutterSdkPath
        }

        includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    if (!providers.gradleProperty("hotfix.prebuilt").isPresent) {
        id("dev.flutter.flutter-plugin-loader") version "1.0.0"
    }
    id("com.android.application") version "8.11.1" apply false
    id("org.jetbrains.kotlin.android") version "2.2.20" apply false
}

include(":app")
if (providers.gradleProperty("hotfix.prebuilt").isPresent) {
    project(":app").buildFileName = "prebuilt.gradle.kts"
}
